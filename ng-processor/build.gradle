/*
 * Copyright 2015 Tyler Davis
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

allprojects {
    repositories {
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }
}

dependencies {
    testCompile 'com.github.davityle:ngandroid:1.0.6-SNAPSHOT'
    compile 'org.apache.velocity:velocity:1.7'
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.testing.compile:compile-testing:0.7'
    testCompile 'com.google.android:android:4.1.1.4'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.github.davityle"
archivesBaseName = "ng-processor"
version = "1.0.6-SNAPSHOT"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'ng-processor'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'annotation processor for NgAndroid'
                url 'https://github.com/davityle/ngandroid'

                scm {
                    connection 'scm:git@github.com:davityle/ngandroid.git'
                    developerConnection 'scm:git@github.com:davityle/ngandroid.git'
                    url 'https://github.com/davityle/ngandroid'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'davityle'
                        name 'Tyler Davis'
                        email 'tyler@goodintheglobe.com'
                    }
                }
            }
        }
    }
}
